import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7.1'
    id 'net.nemerosa.versioning' version '2.3.0'
}

Date buildTimeAndDate = new Date()
ext {
    buildDate = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'junit:junit:4.11'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

def pomConfig = {
    name project.name
    description project.pomDescription
    url project.repositoryUrl
    inceptionYear pomInceptionYear

    scm {
        connection 'scm:git:git@gitlab.com:tastapod/jgotesting.git'
        developerConnection 'scm:git:git@gitlab.com:tastapod/jgotesting.git'
        url 'https://gitlab.com/tastapod/jgotesting'
    }

    licenses {
        license {
            name pomLicenseName
            url pomLicenseUrl
        }
    }

    developers {
        developer {
            id 'dn'
            name 'Dan North'
            email 'dan@dannorth.net'
            url 'http://dannorth.net/'
            timezone 'Europe/London'
        }
    }
}

jar {
    manifest {
        attributes(
                'Created-By': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString(),
                'Build-Date': buildDate,
                'Build-Time': buildTime,
                'Build-Revision': versioning.info.commit,
                'Specification-Title': project.name,
                'Specification-Version': project.version,
                'Specification-Vendor': 'jgotesting.org',
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'jgotesting.org'
        )
    }

    metaInf {
        from rootProject.file('.')
        include 'LICENSE.*'
    }
}

if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = ''
if (!project.hasProperty('bintrayApiKey')) ext.bintrayApiKey = ''

publishing {
    publications {
        mavenCustom(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            // Copied wholesale from https://github.com/griffon/griffon/blob/development/gradle/publish.gradle
            pom.withXml {
                // all dependencies should use the default scope (compile) but
                // Gradle insists in using runtime as default
                asNode().dependencies.dependency.each { dep ->
                    if (dep.scope.text() == 'runtime') {
                        dep.remove(dep.scope)
                    }
                }
                asNode().children().last() + pomConfig
                asNode().appendNode('description', project.pomDescription)
            }
        }
    }
}

bintray {
    user = project.bintrayUsername
    key = project.bintrayApiKey
    publications = ['mavenCustom']
    pkg {
        repo = 'maven'
        userOrg = 'jgotesting'
        name = project.name
        desc = project.pomDescription
        licenses = ['BSD 3-Clause']
        labels = ['testing', 'junit']
        websiteUrl = project.repositoryUrl
        issueTrackerUrl = project.issueTrackerUrl
        vcsUrl = project.vcsUrl
        publicDownloadNumbers = true
    }
}
